grammar org.eclipse.xtext.services.Services with org.eclipse.xtext.xbase.Xbase

generate services "http://www.eclipse.org/xtext/services/Services"

ServicesFile:
	component=Component
;

Component:
	'package' packageName=QualifiedName
	(imports+=Import)*
	('uses' requires+=RequireDeclaration (',' requires+=RequireDeclaration)*)?
	"component" name=ValidID '{'
		(services+=Service)*
	'}'
;


Service:
	(service?='service' '[' (get?='GET' | post?='POST') ']')? name=ValidID'(' (params+=FullJvmFormalParameter(',' params+=FullJvmFormalParameter)*)? ')' 
		':' type=JvmTypeReference
		body=XBlockExpression
;

RequireDeclaration:
	type=JvmTypeReference (name=ID)?
;


Import:
	"import" importedNamespace=QualifiedName
;

